plugins {
    id 'java'
    id 'maven-publish'
}
apply plugin: 'application'

def pkgName = 'org.hertsstack'
def pkgVersion = '1.0.0'
def javaVersion = project.hasProperty('javaVersion') ? project.getProperty('javaVersion') : '11'
def artifactId = 'metrics'

group pkgName
version pkgVersion
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    implementation project(':herts-core')
    implementation 'io.micrometer:micrometer-core:1.10.6'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.10.6'

    implementation 'javax.servlet:servlet-api:3.0-alpha-1'
    implementation ('org.eclipse.jetty:jetty-server:10.0.15') {
        exclude group: "org.slf4j", module: "slf4j-api"
    }
    implementation ('org.eclipse.jetty:jetty-webapp:10.0.0') {
        exclude group: "org.slf4j", module: "slf4j-api"
    }

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
    testLogging {
        events 'started', 'skipped', 'passed', 'failed'
        exceptionFormat = 'full'
    }
}

jar {
    manifest {
        attributes(
                [
                        'Built-By'              : System.getProperty('user.name'),
                        'Built-Date'            : new Date(),
                        'Built-JDK'             : System.getProperty('java.version'),
                        'Implementation-Version': version,
                        'Implementation-Title'  : project.name,
                ]
        )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = pkgName
            artifactId = artifactId
            version = pkgVersion

            from components.java

            pom {
                name = 'Herts metrics library'
                description = 'Herts real time framework for Metrics'
                url = 'https://herts-framework.herts-stack.org/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'tomoyane'
                        name = 'tomoyane'
                        email = 'tomoyane111@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url 'file://Users/tomohito.oyane/Developers/maven-repo'
        }
    }
}
