plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}
apply plugin: 'application'

def pkgName = 'org.hertsstack'
def pkgVersion = '1.0.0'
def javaVersion = project.hasProperty('javaVersion') ? project.getProperty('javaVersion') : '11'
def artifactId = 'gateway'

group pkgName
version pkgVersion
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

repositories {
    mavenCentral()
    gradlePluginPortal()
}

def grpcVersion = '1.57.0'
dependencies {
    implementation project(':herts-core')
    implementation project(':herts-serializer')
    implementation project(':herts-rpc-client')
    implementation project(':herts-http')
    implementation project(':herts-metrics')

    implementation 'com.fasterxml.jackson.core:jackson-core:2.15.2'

    implementation 'javax.servlet:servlet-api:3.0-alpha-1'
    implementation ('org.eclipse.jetty:jetty-server:10.0.15') {
        exclude group: "org.slf4j", module: "slf4j-api"
    }
    implementation ('org.eclipse.jetty:jetty-webapp:10.0.0') {
        exclude group: "org.slf4j", module: "slf4j-api"
    }

    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"

    testImplementation project(':herts-core')
    testImplementation project(':herts-http')
    testImplementation project(':herts-http-client')
    testImplementation project(':herts-metrics')
    testImplementation project(':herts-serializer')
    testImplementation project(':herts-rpc')
    testImplementation project(':herts-rpc-client')
    testImplementation 'com.fasterxml.jackson.core:jackson-annotations:2.15.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
    testLogging {
        events 'started', 'skipped', 'passed', 'failed'
        exceptionFormat = 'full'
    }
}

jar {
    manifest {
        attributes(
                [
                        'Built-By'              : System.getProperty('user.name'),
                        'Built-Date'            : new Date(),
                        'Built-JDK'             : System.getProperty('java.version'),
                        'Implementation-Version': version,
                        'Implementation-Title'  : project.name,
                ]
        )
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = pkgName
            artifactId = artifactId
            version = pkgVersion

            from components.java

            pom {
                name = 'Herts gateway library'
                description = 'Herts real time framework for Gateway'
                url = 'https://github.com/herts-stack/herts'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'herts'
                        name = 'Herts Developer'
                        email = 'tomoyane111@gmail.com'
                    }
                }
                scm {
                    connection.set("git@github.com:herts-stack/herts.git")
                    developerConnection.set("git@github.com:herts-stack/herts.git")
                    url.set("https://github.com/herts-stack")
                }
            }
        }
    }
    repositories {
        maven {
            //url 'file://Users/tomohito.oyane/Developers/maven-repo'
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ""
                password = ""
            }
        }
    }
}

signing {
    sign(publishing.publications["maven"])
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
