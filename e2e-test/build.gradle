plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.graalvm.buildtools.native' version '0.9.21'
}
apply plugin: 'application'

def javaVersion = project.hasProperty('javaVersion') ? project.getProperty('javaVersion') : '17'

group 'org.hertsstack.e2etest'
version '1.0.0'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

repositories {
    mavenCentral()
    gradlePluginPortal()
}

def grpcVersion = '1.53.0'
dependencies {
    implementation project(':herts-core')
    implementation project(':herts-rpc')
    implementation project(':herts-rpc-client')
    implementation project(':herts-http')
    implementation project(':herts-http-client')
    implementation project(':herts-serializer')
    implementation project(':herts-broker')
    implementation project(':herts-broker-local')
    implementation project(':herts-broker-redis')

    implementation 'com.fasterxml.jackson.core:jackson-core:2.15.2'
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    implementation 'commons-cli:commons-cli:1.5.0'
    implementation 'redis.clients:jedis:4.3.1'
}

def mainClassPath = 'org.herts.e2etest.Main'
application {
    mainClass = mainClassPath
}

jar {
    manifest {
        attributes(
                [
                        'Main-Class'            : mainClassPath,
                        'Built-By'              : System.getProperty('user.name'),
                        'Built-Date'            : new Date(),
                        'Built-JDK'             : System.getProperty('java.version'),
                        'Implementation-Version': version,
                        'Implementation-Title'  : project.name,
                ]
        )
    }
}

test {
    useJUnitPlatform()
}

shadowJar {
    mergeServiceFiles()
}

tasks.register('runExample', JavaExec) {
    dependsOn 'classes'
    main = mainClassPath
    classpath = sourceSets.main.runtimeClasspath
}

graalvmNative {
    binaries {
        main {
            javaLauncher = javaToolchains.launcherFor {
                languageVersion = JavaLanguageVersion.of(17)
            }
        }
    }
}