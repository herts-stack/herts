plugins {
    id 'java'
    id 'maven-publish'
}
apply plugin: 'application'

def pkgName = 'org.herts.broker'
def pkgVersion = '1.0.0'
def javaVersion = project.hasProperty('javaVersion') ? project.getProperty('javaVersion') : '11'
def artifactId = 'herts-broker'

group pkgName
version pkgVersion
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

repositories {
    mavenCentral()
    gradlePluginPortal()
}

def grpcVersion = '1.53.0'
dependencies {
    implementation 'org.msgpack:jackson-dataformat-msgpack:0.7.1'
    implementation 'org.msgpack:msgpack:0.6.12'

    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"

    implementation 'com.google.guava:guava:32.0.1-jre'

    testImplementation project(":herts-core")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
    testLogging {
        events 'started', 'skipped', 'passed', 'failed'
        exceptionFormat = 'full'
    }
}

jar {
    manifest {
        attributes(
                [
                        'Built-By'              : System.getProperty('user.name'),
                        'Built-Date'            : new Date(),
                        'Built-JDK'             : System.getProperty('java.version'),
                        'Implementation-Version': version,
                        'Implementation-Title'  : project.name,
                ]
        )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = pkgName
            artifactId = artifactId
            version = pkgVersion

            from components.java

            pom {
                name = 'Herts broker library'
                description = 'Herts real time framework for Broker'
                url = 'https://www.herts.org/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'tomoyane'
                        name = 'tomoyane'
                        email = 'tomoyane111@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url 'file://Users/tomohito.oyane/Developers/maven-repo'
        }
    }
}
